import io.papermc.paperweight.userdev.ReobfArtifactConfiguration

plugins {
    id "java"
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.18"
    id("com.gradleup.shadow") version "9.0.1"
}

group = 'com.minecraftcivilizations'
version = '0.0.0.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = uri("https://maven.pkg.github.com/Emergent-Civilization/MinecraftCivilizationsCore")
        credentials {
            username = System.getenv("GPR_USER") ?: project.findProperty("gpr.user")
            password = System.getenv("GPR_KEY") ?: project.findProperty("gpr.key")
        }
        metadataSources {
            mavenPom()
            artifact()
        }
    }
    maven {
        url = uri("https://maven.pkg.github.com/Emergent-Civilization/Weight")
        credentials {
            username = System.getenv("GPR_USER") ?: project.findProperty("gpr.user")
            password = System.getenv("GPR_KEY") ?: project.findProperty("gpr.key")
        }
        metadataSources {
            mavenPom()
            artifact()
        }
    }
    maven {
        url "https://repo.dmulloy2.net/repository/public/"
        name "protocol-lib"
    }
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
        name = "aikar"
    }
}

configurations {
    shadow
}

dependencies {
    paperweight.paperDevBundle("1.21.8-R0.1-SNAPSHOT")
    compileOnly("com.minecraftcivilizations:minecraftcivilizationscore:0.5.1")

    shadow "com.google.code.gson:gson:2.13.1"
    shadow "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    shadow("net.kyori:adventure-text-minimessage:4.25.0")
    shadow("net.kyori:adventure-api:4.25.0")
    shadow("net.kyori:adventure-text-serializer-legacy:4.25.0")

    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.3.0"
    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")

    testCompileOnly("org.projectlombok:lombok:1.18.38")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.38")
}

jar {
    enabled = false // Disable the default JAR task
}

shadowJar {
    archiveClassifier = ''
    archiveBaseName = project.name

    configurations = [project.configurations.shadow]

    relocate 'co.aikar.commands', 'com.minecraftcivilizations.specialization.acf'
    relocate 'co.aikar.locales', 'com.minecraftcivilizations.specialization.locales'
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.8")
    }
}

def targetJavaVersion = 23
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add("--enable-preview")

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

paperweight.reobfArtifactConfiguration = ReobfArtifactConfiguration.getMOJANG_PRODUCTION()



tasks.register('copyPluginJar', Copy) {
    dependsOn shadowJar
    from(shadowJar.archiveFile)

    def exportPath = project.findProperty("export.path")
    onlyIf { exportPath != null }

    if (exportPath) {
        into(exportPath)
    }
}


tasks.build {
    dependsOn tasks.copyPluginJar
}